plugins {
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'idea'
    id 'application'
    id 'info.solidsoft.pitest' version '1.15.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'

}

apply plugin: 'info.solidsoft.pitest.aggregator' // to 'pitestReportAggregate' appear

group 'com.nsa.cm6123-6623'
version '1.0-SNAPSHOT'

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

repositories {
	mavenCentral()
}

dependencies {

    implementation 'org.apache.logging.log4j:log4j-api:2.23.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.23.1'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.23.1'
		implementation 'com.github.javafaker:javafaker:1.0.2'


    testImplementation(platform('org.junit:junit-bom:5.9.2'))
    testImplementation('org.junit.jupiter:junit-jupiter:5.9.2')
    testImplementation('org.mockito:mockito-core:5.2.0')
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.9.2")

}

//test configuration
test{
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
}
//application configuration

run{
    standardInput = System.in
}

jar {
    manifest {
        attributes ('Main-Class': 'com.cm6123.monopoly.app.Application',
        'Multi-Release':'true')

    }
    from sourceSets.main.allSource

}



application {
    mainClass = 'com.cm6123.monopoly.app.Application'
}

//tool configuration
checkstyle {
    toolVersion = 9.2
    checkstyleMain.enabled = true
    checkstyleTest.enabled = false
}

checkstyleMain {
    configFile = file("config/checkstyle/checkstyleMain.xml")
    maxErrors = 10
}

jacoco {
    toolVersion = "0.8.11"
    //reportsDirectory = layout.buildDirectory.dir('jacocoHtml')
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')

        //html.destination file("${buildDir}/jacocoHtml")
    }
}
jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.5
            }
            excludes = [
                    'com.cm6123.monopoly.app.*'
            ]
        }
    }
}
check.dependsOn jacocoTestCoverageVerification
// create combined task

task buildAndReport {
    dependsOn build, jacocoTestReport
}

rootProject.tasks.named("jar") {
    duplicatesStrategy = 'include'
}

pitest {
    targetClasses = ['com.cm6123.monopoly.*']  //by default "${project.group}.*"
    pitestVersion = '1.9.11' //not needed when a default PIT version should be used
    threads = 4
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    junit5PluginVersion = '1.1.2'

    reportAggregator {
        testStrengthThreshold.set(50)
        mutationThreshold.set(40)
        maxSurviving.set(3)
    }
    //verbose = true



}



